<!doctype html>
<html lang="en">

<head>
  <meta charset="utf-8" />
  <title>WS1 – 3 Specular</title>
  <style>
    canvas {
      width: 640px;
      height: 320px;
      border: 1px solid #333;
      display: block;
      margin: 1rem auto;
    }
  </style>
</head>

<body>
  <h1 style="text-align:center;">Worksheet 2 · Part 3</h1>
  <canvas id="gfx" width="640" height="320"></canvas>
  <div style="max-width: 720px; margin: 0 auto 0.5rem; font: 14px/1.2 system-ui;">
    <label for="gammaSlider"><b>Gamma</b>: </label>
    <input id="gammaSlider" type="range" min="0.8" max="3.0" step="0.01" value="2.2" style="width: 320px;">
    <span id="gammaValue">2.20</span>
    <small style="opacity:.75"> (Scroll on canvas to zoom)</small>
  </div>

  <script defer src="../../common/MV.js"></script>

  <script id="shader-rays" type="x-shader/wgsl">
    struct VSOut {
  @builtin(position) pos : vec4<f32>,
  @location(0) img : vec2<f32>,
};

@vertex
fn vsMain(@builtin(vertex_index) vid : u32) -> VSOut {
  var corners = array<vec2<f32>, 4>(
    vec2<f32>(-1.0, -1.0),
    vec2<f32>( 1.0, -1.0),
    vec2<f32>(-1.0,  1.0),
    vec2<f32>( 1.0,  1.0)
  );
  var out : VSOut;
  let p = corners[vid];
  out.pos = vec4<f32>(p, 0.0, 1.0);
  out.img = p;
  return out;
}

struct Camera {
  eye    : vec4<f32>,
  U      : vec4<f32>,
  V      : vec4<f32>,
  W      : vec4<f32>,
  aspect : f32,
  zoom   : f32,
  gamma  : f32,
  _pad   : f32,
};
@group(0) @binding(0) var<uniform> cam : Camera;

struct Ray    { origin: vec3<f32>, dir: vec3<f32> };

struct HitInfo{
  hit  : bool,
  t    : f32,
  n    : vec3<f32>,
  mat  : Material,
};

struct Light {
  Li : vec3<f32>,
  wi : vec3<f32>,
};

struct Material {
  ambient  : vec3<f32>,
  diffuse  : vec3<f32>,
  specular : vec3<f32>,
  shininess: f32,
};


fn intersectTriangle(ray: Ray,
                     v0: vec3<f32>, v1: vec3<f32>, v2: vec3<f32>,
                     tmin: f32, tmax: f32) -> HitInfo {
  let e0 = v1 - v0;
  let e1 = v2 - v0;
  let n  = cross(e0, e1);

  var q = dot(ray.dir, n);
  if (abs(q) < 1e-8) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0))); }
  q = 1.0 / q;

  let o_to_v0 = v0 - ray.origin;
  let t = dot(o_to_v0, n) * q;
  if (t <= tmin || t >= tmax) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0))); }

  let n_tmp = cross(o_to_v0, ray.dir);
  let beta  =  dot(n_tmp, e1) * q;
  if (beta < 0.0) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0))); }

  let gamma = -dot(n_tmp, e0) * q;
  if (gamma < 0.0 || beta + gamma > 1.0) {
    return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0)));
  }

  let nn  = normalize(n);
  let mat = makeMaterial(vec3<f32>(0.4, 0.3, 0.2), 1.0, vec3<f32>(0.0)); // triangle color
  return HitInfo(true, t, nn, mat);
}

fn intersectSphere(ray: Ray,
                   C: vec3<f32>, r: f32,
                   tmin: f32, tmax: f32) -> HitInfo {
  let oc = ray.origin - C;
  let b = dot(oc, ray.dir);
  let c = dot(oc, oc) - r*r;
  let disc = b*b - c;
  if (disc < 0.0) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 42.0, vec3<f32>(0.0))); }

  let s = sqrt(disc);
  var t = -b - s;
  if (t <= tmin || t >= tmax) {
    t = -b + s;
    if (t <= tmin || t >= tmax) {
      return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 42.0, vec3<f32>(0.0)));
    }
  }
  let Phit = ray.origin + t * ray.dir;
  let nn   = normalize(Phit - C);
  let mat  = makeMaterial(vec3<f32>(0.0), 42.0, vec3<f32>(0.0));
  return HitInfo(true, t, nn, mat);
}

fn intersectPlane(ray: Ray,
                  P0: vec3<f32>, N: vec3<f32>,
                  tmin: f32, tmax: f32) -> HitInfo {
  let denom = dot(N, ray.dir);
  if (abs(denom) < 1e-6) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0))); }
  let t = dot(P0 - ray.origin, N) / denom;
  if (t <= tmin || t >= tmax) { return HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0))); }
  let nn  = normalize(N);
  let mat = makeMaterial(vec3<f32>(0.1, 0.7, 0.0), 1.0, vec3<f32>(0.0)); // plane color
  return HitInfo(true, t, nn, mat);
}

fn samplePointLight(P: vec3<f32>, lightPos: vec3<f32>, intensity: vec3<f32>) -> Light {
  let L  = lightPos - P;
  let r2 = max(dot(L, L), 1e-8);
  return Light(intensity / r2, normalize(L));
}

fn shadeDiffuse(albedo: vec3<f32>, n: vec3<f32>, light: Light) -> vec3<f32> {
  let nl = max(0.0, dot(normalize(n), light.wi));
  return albedo * light.Li * nl;
}

fn makeMaterial(base_rgb: vec3<f32>, shininess: f32, spec_rgb: vec3<f32>) -> Material {
  return Material(0.1 * base_rgb, 0.9 * base_rgb, spec_rgb, shininess);
}

fn intersect_scene(ray: Ray, tmin: f32, tmax: f32) -> HitInfo {
  var best = HitInfo(false, tmax, vec3<f32>(0.0), makeMaterial(vec3<f32>(0.0), 1.0, vec3<f32>(0.0)));

  var h = intersectTriangle(ray,
    vec3<f32>(-0.2, 0.1,  0.9),
    vec3<f32>( 0.2, 0.1,  0.9),
    vec3<f32>(-0.2, 0.1, -0.1),
    tmin, best.t);
  if (h.hit && h.t < best.t) { best = h; }

  h = intersectSphere(ray, vec3<f32>(0.0, 0.5, 0.0), 0.3, tmin, best.t);
  if (h.hit && h.t < best.t) { best = h; }

  h = intersectPlane(ray, vec3<f32>(0.0, 0.0, 0.0), vec3<f32>(0.0, 1.0, 0.0), tmin, best.t);
  if (h.hit && h.t < best.t) { best = h; }

  return best;
}

fn occluded(P: vec3<f32>, Lpos: vec3<f32>) -> bool {
  let eps  = 1e-4;
  let L    = Lpos - P;
  let dist = length(L);
  let dir  = L / dist;

  let ray = Ray(P + eps * dir, dir);

  let h = intersect_scene(ray, eps, dist - eps);
  return h.hit;
}

const MAX_DEPTH : i32 = 3;

fn background(dir: vec3<f32>) -> vec3<f32> {
  return vec3<f32>(0.1, 0.3, 0.6);
}

fn trace(ray0: Ray) -> vec3<f32> {
  var ray = ray0;
  var Lo  = vec3<f32>(0.0);

  // Up to MAX_DEPTH bounces; sphere is treated as a perfect mirror.
  for (var depth: i32 = 0; depth < MAX_DEPTH; depth = depth + 1) {
    let hit = intersect_scene(ray, 1e-4, 1e30);

    if (!hit.hit) {
      // Miss → return background seen by the current ray
      Lo = background(ray.dir);
      break;
    }

    // Face-forward normal
    var N = hit.n;
    if (dot(N, ray.dir) > 0.0) { N = -N; }

    let P = ray.origin + hit.t * ray.dir;

    // Mirror branch (sphere has shininess = 42.0 from your makeMaterial)
    if (hit.mat.shininess >= 40.0) {
      let R   = normalize(reflect(ray.dir, N));
      let eps = 1e-4;
      ray = Ray(P + eps * R, R); // bounce and continue loop
      continue;
    }

    // Matte Lambert with hard shadows (plane & triangle)
    let PI    = 3.1415926535;
    let Lpos  = vec3<f32>(0.0, 1.0, 0.0);
    let light = samplePointLight(P, Lpos, vec3<f32>(PI, PI, PI));

    if (occluded(P, Lpos)) {
      Lo = hit.mat.ambient;
    } else {
      let nl = max(0.0, dot(N, light.wi));
      Lo = hit.mat.ambient + hit.mat.diffuse * light.Li * nl;
    }
    break; // matte surface ends the path
  }

  return Lo;
}

@fragment
fn fsMain(@location(0) img : vec2<f32>) -> @location(0) vec4<f32> {
  // Build camera ray (constant horizontal FOV)
  let center = cam.eye.xyz + cam.zoom * cam.W.xyz;
  let Pimg   = center + img.x * cam.U.xyz + (img.y / cam.aspect) * cam.V.xyz;
  let dir    = normalize(Pimg - cam.eye.xyz);
  let ray    = Ray(cam.eye.xyz, dir);

  let Lo = trace(ray);

  let outRGB = pow(max(Lo, vec3<f32>(0.0)), vec3<f32>(1.0 / cam.gamma));
  return vec4<f32>(outRGB, 1.0);
}
  </script>

  <script type="module" src="main.js"></script>
</body>

</html>